apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.trevjonez.composer'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "org.ligi.survivalmanual"
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 428
        versionName "4.2.8"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "org.ligi.trulesk.BaseTruleskRunner"
        archivesBaseName = "SurvivalManual-$versionName"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "store"
    productFlavors {
        forFDroid {
            dimension "store"
            buildConfigField 'String', 'STORE', '"fdroid"'
        }

        forAmazon {
            dimension "store"
            buildConfigField 'String', 'STORE', '"amazon"'
        }

        forPlay {
            dimension "store"
            buildConfigField 'String', 'STORE', '"play"'
        }
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/*'
    }

    lintOptions {
        warning 'InvalidPackage'
        warning 'MissingTranslation'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

// Android support
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.4.0'

// Third party
    implementation "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:initializer:$kotpref_version"

    implementation('com.github.ligi:intellij-markdown:android_via_jitpack')
    implementation 'com.github.ligi:TouchImageView:2.1'
    implementation 'com.github.ligi:ExtraCompats:1.0'
    implementation 'com.github.ligi:tracedroid:4.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.github.ligi:KAXT:1.0'

    forPlayImplementation "com.github.ligi.snackengage:snackengage-playrate:$snackengage_version"
    forFDroidImplementation "com.github.ligi.snackengage:snackengage-playrate:$snackengage_version"
    forAmazonImplementation "com.github.ligi.snackengage:snackengage-amazonrate:$snackengage_version"

    implementation 'org.permissionsdispatcher:permissionsdispatcher:4.8.0'
    kapt 'org.permissionsdispatcher:permissionsdispatcher-processor:4.8.0'


// Testing
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.appcompat:appcompat:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation "androidx.test:core:$androidx_test_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version"

    androidTestImplementation 'com.github.ligi:trulesk:4.0'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    androidTestImplementation 'org.mockito:mockito-core:3.10.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.0'

    androidTestUtil 'com.linkedin.testbutler:test-butler-app:2.1.1'

    testImplementation "androidx.test:core:$androidx_test_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    testImplementation 'androidx.annotation:annotation:1.1.0'
}

composer {
    apkInstallTimeout 600
}
